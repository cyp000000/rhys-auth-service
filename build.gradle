buildscript {
	ext {
		springBootVersion = '1.3.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('com.bmuschko:gradle-docker-plugin:2.6.3')
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

jar {
	baseName = 'oauth-service'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.M4"
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile("org.springframework.cloud:spring-cloud-starter-oauth2")
	compile("org.springframework.cloud:spring-cloud-starter-security")
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-data-jpa")
	compile("org.jadira.usertype:usertype.core:4.0.0.GA")
	compile("joda-time:joda-time")
	runtime("mysql:mysql-connector-java")
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

def dockerPort = 9999

import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
	destFile = project.file('build/Dockerfile')
	from 'ofayau/ejre:8-jre'
	maintainer 'Jomo Lumina <jomo@infowarestudios.co.za>'
	exposePort dockerPort
	copyFile "libs/${jar.baseName}-${jar.version}.jar", "${jar.baseName}-${jar.version}.jar"
	copyFile 'resources/main/infoware.p12', 'infoware.p12'

	environmentVariable "TZ","Africa/Johannesburg"
	runCommand("ln -snf /usr/share/zoneinfo/\$TZ /etc/localtime && echo \$TZ > /etc/timezone")

	entryPoint 'java','-jar',"-Dspring.profiles.active=dev","${jar.baseName}-${jar.version}.jar"
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}
